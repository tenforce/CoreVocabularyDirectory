<?xml version="1.0" encoding="utf-8"?>
<!--
  Common XSLT templates

  Copyright 2014 European Union
  Author: Vianney le ClÃ©ment de Saint-Marcq (PwC EU Services)

  Licensed under the EUPL, Version 1.1 or - as soon they
  will be approved by the European Commission - subsequent
  versions of the EUPL (the "Licence");
  You may not use this work except in compliance with the
  Licence.
  You may obtain a copy of the Licence at:
  http://ec.europa.eu/idabc/eupl

  Unless required by applicable law or agreed to in
  writing, software distributed under the Licence is
  distributed on an "AS IS" basis,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
  express or implied.
  See the Licence for the specific language governing
  permissions and limitations under the Licence.

-->
<xsl:stylesheet version="1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:ns="http://semic.eu/namespaces"
  xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
  xmlns:rdfs="http://www.w3.org/2000/01/rdf-schema#"
  xmlns:locn="http://www.w3.org/ns/locn#"
  xmlns:skos="http://www.w3.org/2004/02/skos/core#"
  xmlns:vdm="http://vocabs.tenforce.com/def#"
  exclude-result-prefixes="xsl ns rdf rdfs locn">

  <xsl:param name="baseuri" select="'%BASEURI%'" />
  <xsl:param name="subject-uri" />
  <xsl:variable name="namespaces" select="document('%ROOTURI%xslt/namespaces.xml')" />

  <xsl:variable name="subject" select="//rdf:Description[@rdf:about=$subject-uri]" />
  <xsl:variable name="subject-label">
    <xsl:choose>
      <xsl:when test="$subject/rdfs:label">
        <xsl:value-of select="string($subject/rdfs:label)" />
      </xsl:when>
      <xsl:otherwise>
        <xsl:call-template name="to-curie">
          <xsl:with-param name="uri" select="$subject-uri" />
          <xsl:with-param name="strip" select="true()" />
        </xsl:call-template>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:variable>

  <xsl:output method="html" indent="yes" encoding="UTF-8" />

  <!-- Standard HTML boilerplate -->
  <xsl:template name="html">
    <xsl:param name="title" />
    <xsl:param name="body" />
    <xsl:text disable-output-escaping='yes'>&lt;!DOCTYPE html></xsl:text>
    <html>
      <head>
        <meta charset="UTF-8" />
        <title><xsl:value-of select="$title" /></title>
        <meta name="viewport" content="width=device-width,initial-scale=1" />
        <link rel="stylesheet" type="text/css" href="/vdm/css/normalize.css" />
        <link rel="stylesheet" type="text/css" href="http://fonts.googleapis.com/css?family=Source+Sans+Pro:400,400italic,600,600italic,300" />
        <link rel="stylesheet" type="text/css" href="/vdm/css/screen.css" />
        <script type="text/javascript" src="http://code.jquery.com/jquery-1.11.0.min.js"></script>
      </head>
      <body>
        <header>
          <div class="logo">
            <a href="https://www.semic.eu">
              <img src="/vdm/images/semic_logo.png" alt="SEMIC logo" width="90" height="90" />
            </a>
          </div>
          <h1><a href="/vdm/">Core Data Model Mapping Directory</a></h1>
        </header>

        <xsl:copy-of select="$body" />
        <footer>
          <p>This work is supported by
          <a href="http://ec.europa.eu/isa/actions/01-trusted-information-exchange/1-1action_en.htm" target="_blank">Action 1.1</a>
          of the
          <a href="http://ec.europa.eu/isa/" target="_blank">Interoperability Solutions
          for European Public Adminstrations (ISA)</a> Programme of the European
          Commission.</p>

          <p><strong>Linked Data pilots: </strong>
            <a href="http://location.testproject.eu/BEL">Core Location pilot</a> |
            <a href="http://cpsv.testproject.eu/CPSV">Core Public Service pilot</a> |
            <a href="http://org.testproject.eu/MAREG">Organisation Ontology pilot</a> |
            <a href="http://health.testproject.eu/PPP">Plant Protection Products pilot</a> |
            <a href="http://maritime.testproject.eu/CISE">Maritime Surveillance pilot</a>
          </p>

          <p>
            <a href="https://joinup.ec.europa.eu/asset/dcat_application_profile/description" target="_blank"><img alt="DCAT application profile for European data portals" src="https://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/DCAT_application_profile_for_European_data_portals_logo_0.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="https://joinup.ec.europa.eu/asset/adms/description" target="_blank"><img alt="Asset Description Metadata Schema (ADMS)" src="http://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/adms_logo.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="https://joinup.ec.europa.eu/asset/adms_foss/description" target="_blank"><img alt="Asset Description Metadata Schema for Software (ADMS.SW)" src="http://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/ADMS_SW_Logo.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="https://joinup.ec.europa.eu/asset/core_business/description" target="_blank"><img alt="Core Business Vocabulary" src="http://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/core_business_logo.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="https://joinup.ec.europa.eu/asset/core_person/description"><img alt="Core Person Vocabulary" src="http://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/core_person_logo.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="https://joinup.ec.europa.eu/asset/core_location/description" target="_blank"><img alt="Core Location Vocabulary" src="http://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/core_location_logo.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="https://joinup.ec.europa.eu/asset/core_public_service/description" target="_blank"><img alt="Core Public Service Vocabulary" src="https://joinup.ec.europa.eu/sites/default/files/imagecache/community_logo/core_public_service_logo.png" width="70" height="70" /></a>
            <xsl:text> </xsl:text>
            <a href="http://ec.europa.eu/isa/" target="_blank"><img alt="isa" src="http://joinup.ec.europa.eu/sites/default/files/ckeditor_files/images/isa_logo.png" width="70" height="70" /></a>
          </p>
        </footer>
        <script type="text/javascript"><xsl:text>
          var _gaq = _gaq || [];
          _gaq.push(['_setAccount', '%GOOGLE_ANALYTICS%']);
          _gaq.push(['_trackPageview']);

          (function() {
          var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
          ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
          var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
          })();
        </xsl:text></script>
      </body>
    </html>
  </xsl:template>

  <!-- Standard about page boilerplate -->
  <xsl:template name="about">
    <xsl:call-template name="html">
      <xsl:with-param name="title">
        About: <xsl:value-of select="$subject-label" />
      </xsl:with-param>
      <xsl:with-param name="body">
        <xsl:choose>
          <xsl:when test="$subject">
            <xsl:call-template name="content" />
          </xsl:when>
          <xsl:otherwise>
            <section>
              <h2>About: <xsl:value-of select="$subject-label" /></h2>
              <div class="error">Sorry, we do not know about this resource.</div>
            </section>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:with-param>
    </xsl:call-template>
  </xsl:template>

  <!-- Add a small RDF description about $subject with links to raw data. -->
  <xsl:template name="rdf-info">
    <dl class="properties">
			<dt>Internal Identifier</dt>
			<dd>
        <xsl:choose>
					<xsl:when test="$subject/vdm:has_internalidentifier">
						<xsl:call-template name="rdf-value">
							<xsl:with-param name="element" 
															select="$subject/vdm:has_internalidentifier"/>
							<xsl:with-param name="strip-uri" select="true()" />
						</xsl:call-template>
					</xsl:when>
					<xsl:otherwise>
						<xsl:text>No internal identifier</xsl:text>
					</xsl:otherwise>
				</xsl:choose>
			</dd>
			<dt>Public ID</dt>
			<dd>
				<xsl:choose>
					<xsl:when test="$subject/vdm:has_publicidentifier">
						<a href="{$subject/vdm:has_publicidentifier/@rdf:resource}" 
							 title="This is the Public Identifier for this resource">
							<xsl:value-of select="$subject/vdm:has_publicidentifier/@rdf:resource"/>
						</a>
					</xsl:when>
					<xsl:when test="not($subject/vdm:has_internalidentifier)">
						<a href="{$subject-uri}" 
							 title="This is the Public Identifier for this resource">
							<xsl:value-of select="$subject-uri"/>
						</a>
					</xsl:when>
					<xsl:otherwise>
						<xsl:text>No public identifier</xsl:text>
					</xsl:otherwise>
				</xsl:choose>
			</dd>
      <dt>CDMMD ID</dt>
			<dd>
				<xsl:choose>
					<xsl:when test="$subject/vdm:has_internalidentifier">
						<a href="{$subject-uri}" title="This is the Core Data Model Mapping Directory (CDMMD) Identifier, which will always point to this page"><xsl:value-of select="$subject-uri" /></a>
					</xsl:when>
					<xsl:otherwise>
						<xsl:text>No CDMMD Id</xsl:text>
					</xsl:otherwise>
				</xsl:choose>
				</dd>
      <xsl:if test="$subject/rdf:type">
        <dt>Type</dt>
        <dd>
          <xsl:call-template name="rdf-value">
            <xsl:with-param name="element" select="$subject/rdf:type" />
            <xsl:with-param name="strip-uri" select="true()" />
          </xsl:call-template>
        </dd>
      </xsl:if>
      <dt>Raw data</dt>
      <dd>
        <a>
          <xsl:attribute name="href">
            <xsl:call-template name="about-uri">
              <xsl:with-param name="uri" select="$subject-uri" />
            </xsl:call-template>
          </xsl:attribute>
          HTML
        </a>
        <xsl:text> | </xsl:text>
        <a>
          <xsl:attribute name="href">
            <xsl:call-template name="resolve-uri">
              <xsl:with-param name="uri" select="$subject-uri" />
              <xsl:with-param name="format">rdf</xsl:with-param>
            </xsl:call-template>
          </xsl:attribute>
          RDF/XML
        </a>
        <xsl:text> | </xsl:text>
        <a>
          <xsl:attribute name="href">
            <xsl:call-template name="resolve-uri">
              <xsl:with-param name="uri" select="$subject-uri" />
              <xsl:with-param name="format">ttl</xsl:with-param>
            </xsl:call-template>
          </xsl:attribute>
          Turtle
        </a>
      </dd>
    </dl>
  </xsl:template>

  <!-- Print the URI of the property, with link and label if possible. -->
  <xsl:template name="rdf-property">
    <xsl:param name="element" />
    <xsl:param name="strip-uri" select="false()" />
    <xsl:call-template name="rdf-link">
      <xsl:with-param name="uri" select="concat(namespace-uri($element), local-name($element))" />
      <xsl:with-param name="strip" select="$strip-uri" />
    </xsl:call-template>
  </xsl:template>

	<!-- Variant of rdf-property, except that references to skod:broardMatch and
       skos:narrowMatch are replaced with their inverse property name -->

  <xsl:template name="rdf-inverse-property">
    <xsl:param name="element" />
    <xsl:param name="strip-uri" select="false()" />
		
		<xsl:variable name="uri">
			<xsl:choose>
				<xsl:when test="local-name($element) = 'broadMatch'">
					<xsl:value-of select="concat(namespace-uri($element), 'narrowMatch')"/>
				</xsl:when>
				<xsl:when test="local-name($element) = 'narrowMatch'">
					<xsl:value-of select="concat(namespace-uri($element), 'broadMatch')"/>
				</xsl:when>
				<xsl:otherwise>
					<xsl:value-of 
							select="concat(namespace-uri($element), local-name($element))" />
				</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>

    <xsl:call-template name="rdf-link">
      <xsl:with-param name="uri" select="$uri"/>
      <xsl:with-param name="strip" select="$strip-uri" />
    </xsl:call-template>
  </xsl:template>

  <!-- Print the value of an element, with link and label if possible. -->
  <xsl:template name="rdf-value">
    <xsl:param name="element" />
    <xsl:param name="strip-uri" select="true()" />
    <xsl:variable name="uri" select="string($element/@rdf:about | $element/@rdf:resource)" />
    <xsl:variable name="node-id" select="string($element/@rdf:nodeID)" />
    <xsl:choose>
      <!-- URI -->
      <xsl:when test="$uri">
        <xsl:call-template name="rdf-link">
          <xsl:with-param name="uri" select="$uri" />
          <xsl:with-param name="strip" select="$strip-uri" />
        </xsl:call-template>
      </xsl:when>
      <!-- Blank node -->
      <xsl:when test="$node-id">
        <xsl:text>(</xsl:text>
        <xsl:value-of select="$node-id" />
        <xsl:text>)</xsl:text>
      </xsl:when>
      <!-- Literal -->
      <xsl:otherwise>
        <xsl:value-of select="$element" />
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

	<!--  -->
  <xsl:template name="rdf-context">
    <xsl:param name="id" />
    <xsl:param name="strip-uri" select="false()" />

		<xsl:variable name="cc" 
									select="//rdf:Description[@rdf:about=$id]/*[self::vdm:context or self::vdm:has_datamodel]/@rdf:resource"/>
    <xsl:call-template name="rdf-value">
      <xsl:with-param name="element" 
											select="//rdf:Description[@rdf:about=$cc]" />
    </xsl:call-template>
	</xsl:template>

  <xsl:template name="rdf-context2">
    <xsl:param name="id" />
    <xsl:param name="strip-uri" select="false()" />
    <xsl:call-template name="rdf-value">
      <xsl:with-param name="element" 
											select="//rdf:Description[@rdf:about=$id]" />
    </xsl:call-template>
	</xsl:template>

  <!-- Add a HTML link for a resource, with the rdfs:label if it exists. -->
  <xsl:template name="rdf-link">
    <xsl:param name="uri" />
    <xsl:param name="strip" select="true()" /><!-- fallback to strip-uri? -->
    <xsl:variable name="label" select="//rdf:Description[@rdf:about=string($uri)]/rdfs:label" />
    <a>
      <xsl:attribute name="href">
        <xsl:call-template name="resolve-uri">
          <xsl:with-param name="uri" select="$uri" />
        </xsl:call-template>
      </xsl:attribute>
      <xsl:choose>
        <xsl:when test="$label">
          <xsl:value-of select="$label" />
        </xsl:when>
        <xsl:otherwise>
          <xsl:call-template name="to-curie">
            <xsl:with-param name="uri" select="$uri" />
            <xsl:with-param name="strip" select="$strip" />
          </xsl:call-template>
        </xsl:otherwise>
      </xsl:choose>
    </a>
  </xsl:template>

  <!-- Print the CURIE version of a URI using the namespaces defined in an
  external document. -->
  <xsl:template name="to-curie">
    <xsl:param name="uri" />
    <xsl:param name="strip" select="false()" /><!-- fallback to strip-uri? -->
    <xsl:variable name="ns" select="$namespaces//ns:namespace[starts-with(string($uri), string(ns:uri))]" />
    <xsl:choose>
      <xsl:when test="$ns">
        <xsl:value-of select="$ns/ns:prefix" />
        <xsl:text>:</xsl:text>
        <xsl:value-of select="substring(string($uri), string-length($ns/ns:uri) + 1)" />
      </xsl:when>
      <xsl:when test="$strip">
        <xsl:call-template name="strip-uri">
          <xsl:with-param name="uri" select="$uri" />
        </xsl:call-template>
      </xsl:when>
      <xsl:otherwise>
        <xsl:value-of select="string($uri)" />
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Strip a URI to its last component. E.g., http://example.com/id/test/
  would become test, and http://example.com/def#property would become property.
  -->
  <xsl:template name="strip-uri">
    <xsl:param name="uri" />
    <xsl:choose>
      <xsl:when test="contains(string($uri), '/')">
        <xsl:choose>
          <xsl:when test="substring-after(string($uri), '/') = ''">
            <xsl:value-of select="substring-before(string($uri), '/')" />
          </xsl:when>
          <xsl:otherwise>
            <xsl:call-template name="strip-uri">
              <xsl:with-param name="uri" select="substring-after(string($uri), '/')" />
            </xsl:call-template>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:when>
      <xsl:otherwise>
        <xsl:choose>
          <xsl:when test="contains(string($uri), '#')">
            <xsl:value-of select="substring-after(string($uri), '#')" />
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="string($uri)" />
          </xsl:otherwise>
        </xsl:choose>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Return a resolvable version of URI, without going through /about if
  possible. -->
  <xsl:template name="resolve-uri">
    <xsl:param name="uri" />
    <xsl:param name="format">html</xsl:param>
    <xsl:choose>
      <xsl:when test="starts-with(string($uri), concat($baseuri, '/vdm/id/'))">
        <xsl:choose>
          <xsl:when test="$format = 'html'">
            <xsl:value-of select="string($uri)" />
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="$baseuri" />
            <xsl:text>doc</xsl:text>
            <xsl:value-of select="substring(string($uri), string-length($baseuri)+3)" />
            <xsl:text>.</xsl:text>
            <xsl:value-of select="$format" />
          </xsl:otherwise>
        </xsl:choose>
      </xsl:when>
      <xsl:otherwise>
        <xsl:call-template name="about-uri">
          <xsl:with-param name="uri" select="$uri" />
          <xsl:with-param name="format" select="$format" />
        </xsl:call-template>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Return URI resolved though the /about service. -->

  <xsl:template name="about-uri">
    <xsl:param name="uri" />
    <xsl:param name="format">html</xsl:param>

    <xsl:text>/vdm/about/</xsl:text>
    <xsl:value-of select="$format" />
    <xsl:text>/</xsl:text>
    <xsl:call-template name="urlencode">
      <xsl:with-param name="value" select="string($uri)" />
    </xsl:call-template>
  </xsl:template>

  <!-- URL-encode a value -->
	<xsl:variable name="url-hex" select="'0123456789ABCDEF'"/>
	<xsl:variable name="url-ascii"> !"#$%&amp;'()*+,-./0123456789:;&lt;=&gt;?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`abcdefghijklmnopqrstuvwxyz{|}~</xsl:variable>
	<xsl:variable name="url-safe">!'()*-.0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ_abcdefghijklmnopqrstuvwxyz~</xsl:variable>
  <xsl:template name="urlencode">
    <xsl:param name="value" />
    <xsl:if test="$value">
      <xsl:variable name="char" select="substring($value,1,1)" />
      <xsl:choose>
        <xsl:when test="contains($url-safe, $char)">
          <xsl:value-of select="$char" />
        </xsl:when>
        <xsl:when test="contains($url-ascii, $char)">
          <xsl:variable name="codepoint" select="string-length(substring-before($url-ascii,$char)) + 32" />
          <xsl:variable name="hex-digit1" select="substring($url-hex, floor($codepoint div 16) + 1,1)" />
          <xsl:variable name="hex-digit2" select="substring($url-hex, $codepoint mod 16 + 1,1)" />
          <xsl:value-of select="concat('%', $hex-digit1, $hex-digit2)" />
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="$char" />
        </xsl:otherwise>
      </xsl:choose>
      <xsl:call-template name="urlencode">
        <xsl:with-param name="value" select="substring($value, 2)" />
      </xsl:call-template>
    </xsl:if>
  </xsl:template>

</xsl:stylesheet>
<!-- vim:set ts=2 sw=2 et: -->
